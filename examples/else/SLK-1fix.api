// RFID protocol SLK'06


symbols h/1,pair/2, fst/1, snd/1;
private k,IDA,IDB,PINA,PINB,TS1,TS2,void;
channels C1,C2, CR1,CR2;
var X,Y,X1,Y1,Z1,X2,Y2,Z2;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;


//  Alice-Bob notation
// R --> T: Timestamp TS
// T --> R: <r1, h(r0+r1+k)>
// R --> T: h(h(r0+r1+k)+k+r0)
// R and T update their key k with h(k)


Treader1c = 
	out(CR1,pair(h(pair(k,TS1)),TS1)).
	in(CR1,Z1).
	  ([Z1=h(IDA)].
	  out(CR1,h(pair(IDA,PINA)))
	++ [Z1=h(IDB)].
	  out(CR1,h(pair(IDB,PINB))));
	  
Treader1a = 
	out(CR1,pair(h(pair(k,TS1)),TS1)).
	in(CR1,Z1).
	[Z1=h(IDB)].
	  out(CR1,h(pair(IDB,PINB)));
	  
Treader1b = 
	out(CR1,pair(h(pair(k,TS1)),TS1)).
	in(CR1,Z1).
	[Z1=h(IDB)].
	out(CR1,h(pair(IDB,PINB)));
	  
Treader2A = 
	out(CR2,pair(h(pair(k,TS2)),TS2)).
	in(CR2,Z2).
	[Z2=h(IDA)].
	out(CR2,h(pair(IDA,PINA))); 
	
Treader2Au = 
	out(CR2,pair(h(pair(k,TS2)),TS2)).
	in(CR2,Z2).
	[Z2=h(h(pair(IDA,pair(PINA,TS1))))].
	out(CR2,h(pair(h(pair(IDA,pair(PINA,TS1))),PINA))) ;
	
Treader2B = 
	out(CR2,pair(h(pair(k,TS2)),TS2)).
	in(CR2,Z2).
	[Z2=h(IDB)].
	out(CR2,h(pair(IDB,PINB))) ;
	
Treader2 = Treader2A ++ Treader2B;
Treader2u = Treader2Au ++ Treader2B;

TtagA1i = 
	in(C1,X1);

TtagA1a =
	[fst(X1)=h(pair(k,snd(X1)))].
	out(C1,h(IDA));
	
TtagA1c=
	TtagA1i::
	TtagA1a::
	in(C1,Y1);

TtagA1d=
	TtagA1a::
	in(C1,Y1);

TtagA2 = 
	in(C2,X2).
	[fst(X2)=h(pair(k,snd(X2)))].
	if snd(X2)!=snd(X1)
	then
		out(C2,h(IDA))
	else
		out(C2,h(void));
TtagA2u = 
	if Y1=h(pair(IDA,PINA))
	then
		in(C2,X2).
		[fst(X2)=h(pair(k,snd(X2)))].
		if snd(X2)!=snd(X1)
		then
			out(C2,h(pair(h(pair(IDA,pair(PINA,TS1))),PINA)))
		else
			out(C2,h(void))
	else
		TtagA2;


TtagB2 = in(C2,X2).
	[fst(X2)=h(pair(k,snd(X2)))].
	out(C2,h(IDB));



Psame = 
	   (((TtagA1i::(TtagA1a >> 0)) || (Treader1b >> 0)) :: (TtagA2 || Treader2))
	++ ((TtagA1c || (Treader1b >> 0)) :: (TtagA2u || Treader2))
	++ (((TtagA1i::(TtagA1a >> 0)) || Treader1c) :: (TtagA2 || Treader2u))
	++ ((TtagA1c || Treader1c) :: (TtagA2u || Treader2u));
	
Pdiff = 
	   (((TtagA1i::(TtagA1d >> 0)) || (Treader1b >> 0)) :: (TtagB2 || Treader2))
	++ (((TtagA1i::(TtagA1d >> 0)) || Treader1c) :: (TtagB2 || Treader2u));


equivalentct? Psame and Pdiff;
