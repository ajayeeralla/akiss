// RFID protocol LAK'06

#set xor;

symbols h/1,pair/2, fst/1, snd/1;
private kA, kB, k0, r10, r11, r20, r21;
channels C1,C2, CR1,CR2;
var X,Y,X1,Y1,Z1,X2,Y2,Z2;

rewrite fst(pair(X,Y)) -> X;
rewrite snd(pair(X,Y)) -> Y;


//  Alice-Bob notation
// R --> T: r0
// T --> R: <r1, h(r0+r1+k)>
// R --> T: h(h(r0+r1+k)+k+r0)
// R and T update their key k with h(k)


Treader1c = 
	out(CR1,r10).
	in(CR1,Z1).
	  ([snd(Z1)=h(r10+fst(Z1)+kA)].
	  out(CR1,h(snd(Z1)+kA+r10)) 
	++ [snd(Z1)=h(r10+fst(Z1)+kB)].
	  out(CR1,h(snd(Z1)+kB+r10)));
	  
Treader1a = 
	out(CR1,r10).
	in(CR1,Z1).
	[snd(Z1)=h(r10+fst(Z1)+kA)].
	out(CR1,h(snd(Z1)+kA+r10));
	  
Treader1b = 
	out(CR1,r10).
	in(CR1,Z1).
	[snd(Z1)=h(r10+fst(Z1)+kB)].
	out(CR1,h(snd(Z1)+kB+r10));
	  
Treader2A = 
	out(CR2,r20).
	in(CR2,Z2).
	[snd(Z2)=h(r20+fst(Z2)+kA)].
	out(CR2,h(snd(Z2)+kA+r20)); 
	
Treader2Au = 
	out(CR2,r20).
	in(CR2,Z2).
	[snd(Z2)=h(r20+fst(Z2)+h(kA))].
	out(CR2,h(snd(Z2)+h(kA)+r20)) ;
	
Treader2B = 
	out(CR2,r20).
	in(CR2,Z2).
	[snd(Z2)=h(r20+fst(Z2)+kB)].
	out(CR2,h(snd(Z2)+kB+r20)) ;
	
Treader2 = Treader2A ++ Treader2B;
Treader2u = Treader2Au ++ Treader2B;

TtagA1a = 
	in(C1,X1).
	out(C1,pair(r11,h(X1+r11+kA)));
TtagA1c = 
	TtagA1a :: 
	in(C1,Y1);

TtagA2 = 
	in(C2,X2).
	out(C2,pair(r21,h(X2+r21+kA)));
TtagA2u = 
	if Y1=h(h(X1+r11+kA)+kA+X1)
	then
		in(C2,X2).
		out(C2,pair(r21,h(X2+r21+h(kA))))
	else
		TtagA2;


TtagB2 = in(C2,X2).out(C2,pair(r21,h(X2+r21+kB)));



Psame = 
	   ((TtagA1a || Treader1b) >> (TtagA2 || Treader2))
	++ ((TtagA1c || (Treader1b >> 0)) :: (TtagA2u || Treader2))
	++ (((TtagA1a >> 0) || Treader1a) :: (TtagA2 || Treader2u))
	++ ((TtagA1c || Treader1a) :: (TtagA2u || Treader2u));
	
Pdiff = 
	   (((TtagA1c >> 0) || (Treader1b >> 0)) :: (TtagB2 || Treader2))
	++ (((TtagA1c >> 0) || Treader1a) :: (TtagB2 || Treader2u));


equivalentct? Psame and Pdiff;
